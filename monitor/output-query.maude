load apmaude
  *** P *** file name format: pi-"system spec file"
load pi-query
  *** P *** file name format: fixed
load sampling-lib

mod SORTS is
  inc SCHEDULING .
  pr NAT .
  
  sorts Query Queries Key Value Timestamp Data Oids .
  subsort Data < Payload .
  subsort Nat < Timestamp .
  subsort Nat < Value .
  subsort Query < Queries < Payload .
  subsort Oid < Oids .

  op <_,_> : Value Timestamp -> Data [ctor] .
  op null : -> Data .
  
  op read : Key -> Query .
  
  op ept : -> Queries .
  op _::_ : Queries Queries -> Queries [assoc id: ept] .

  op empty : -> Oids .
  op _;_ : Oids Oids -> Oids [assoc comm id: empty] .  
endm
view Key from TRIV to SORTS is sort Elt to Key . endv
view Data from TRIV to SORTS is sort Elt to Data . endv
view Oids from TRIV to SORTS is sort Elt to Oids . endv

mod OBJS is
  inc SORTS .
  pr MAP{Key,Data} .
  pr MAP{Key,Oids} .
  
  ops Client Server Table : -> ObjType .
  
  op queries:_ : Queries -> Attribute .
  op waiting:_ : Oids  -> Attribute .
  op read:_    : Data -> Attribute .
  op replicas:_ : Map{Key,Oids} -> Attribute .  
  op database:_ : Map{Key,Data} -> Attribute .
endm

  *** P *** renaming: P-
mod P-QUERY is
  inc OBJS .
  *** P *** module name format: PI-"system spec module"
  inc PI-QUERY .
  *** P *** file name format: fixed
  inc SAMPLING-LIB .
  
  
  vars O O' TB : Oid .  vars QS QS' : Queries .
  var OS : Oids .  var R : Map{Key,Oids} .
  vars AS AS' : Attributes .  var K : Key .
  var DB : Map{Key,Data} .  vars V V' : Value .
  var P : Payload .  vars DAT DAT' : Data .  
  vars TS TS' : Timestamp .
  
  *** P *** new variables 
  var @T : Float .    var @OBJS : Objects .
  var @DMS : DMsgs .  var @C : Config .
  var @M : Msg .      var @ML : MsgList .


  rl [req] :
     (to O : QS')
     < O : Client | queries: QS, AS > 
  =>
     < O : Client | queries: (QS :: QS'), AS > .
	
	
  *** P ***
  rl [issue] :
     {
     < O : Client | queries: (read(K) :: QS), waiting: empty, read: null, replicas: R, AS > 
	 @OBJS @DMS | @T }
  => 
     {
     < O : Client | queries: (read(K) :: QS), waiting: (FKR[K]), read: null, replicas: R, AS > 
	 delay-issue(O,K,QS,R,AS,sort(propagate read(K) to R[K] from O))
     @OBJS @DMS | @T } .  
	 
  *** P ***
  op delay-issue : Oid Key Queries Map{Key,Oids} Attributes MsgList -> DTask .

  rl [delay-issue-1] :
     { delay-issue(O,K,QS,R,AS,(@M :: @ML)) @C | @T }
  =>
     { delay-issue(O,K,QS,R,AS,@ML) @C 
     [@T + (delta-issue(O,K,QS,R,AS)[sample(lognormal(mSize(K),0.1))[rand]]), @M] | @T } .

  rl [delay-issue-2] : delay-issue(O,K,QS,R,AS,nil) => null .
  
  --- if LHS no msg 
  eq objectEnabled(< O : Client | queries: (read(K) :: QS), waiting: empty, read: null, replicas: R, AS >)
   = true .


  op propagate_to_from_ : Payload Oids Oid -> Msgs . 
  eq propagate P to (O ; OS) from O' = (propagate P to OS from O') (to O from O' : P) .
  eq propagate P to empty from O' = null .


  *** P ***
  rl [reply] :
     {
     (to O from O' : read(K)) 
     < O : Server | database: DB, AS >
	 @OBJS @DMS | @T }
  => 
     {
     < O : Server | database: DB, AS > 
	 delay-reply(O,O',K,DB,AS,sort(to O' from O : DB[K]))
     @OBJS @DMS | @T } . 

  *** P ***	 
  op delay-reply : Oid Oid Key Map{Key,Data} Attributes MsgList -> DTask .	 

  rl [delay-reply-1] :
     { delay-reply(O,O',K,DB,AS,(@M :: @ML)) @C | @T }
  =>
     { delay-reply(O,O',K,DB,AS,@ML) @C 
     [@T + (delta-reply(O,O',K,DB,AS)[sample(lognormal(mSize(DB[K]),0.1))[rand]]), @M] | @T } .
  
  rl [delay-reply-2] : delay-reply(O,O',K,DB,AS,nil) => null .	 


  rl [update] :
     (to O from O' : DAT') 
     < O : Client | read: DAT, waiting: (O' ; OS), AS >
  => 
     < O : Client | read: latest(DAT,DAT'), waiting: OS, AS > .
	 
  op latest : Data Data -> Data .
  eq latest(< V,TS >,null) = < V,TS > .
  eq latest(null,< V,TS >) = < V,TS > .
  eq latest(< V,TS >,< V',TS' >) = if TS >= TS' then < V,TS > else < V',TS' > fi .
   
 crl [finish] :
     < O : Client | queries: (read(K) :: QS), waiting: empty, read: DAT, AS >
  => 
     < O : Client | queries: QS, waiting: empty, read: null, AS > 
     if DAT =/= null .
	 
  *** P ***	 
  ceq objectEnabled(< O : Client | queries: (read(K) :: QS), waiting: empty, read: DAT, AS >)
    = true if DAT =/= null .
endm